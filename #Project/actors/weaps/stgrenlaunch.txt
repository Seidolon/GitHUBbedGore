Actor STGrenadeLauncher : Weapon 30122
{
//$Category New Weapons
Tag "SkullTag Grenade Launcher"
Game Doom
Radius 20
Height 16
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
+WEAPON.STAFF2_KICKBACK
Weapon.AmmoUse 1
Weapon.AmmoUse2 1
Weapon.AmmoGive 2
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoType2 "RocketAmmo"
Weapon.SlotNumber 5
Inventory.PickupMessage "You got the grenade launcher!"
Obituary "%o caught %k's grenade."
States
	{
	Spawn:
	GLAU A -1
	Stop
	Ready:
	GRLG A 1 A_WeaponReady
	Loop
	Deselect:
	GRLG A 1 A_Lower
	Loop
	Select:
	GRLG A 1 A_Raise
	Loop
	Fire:
	GRLG B 8 A_GunFlash
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	GRLG B 12 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
	GRLG B 0 A_ReFire
	Goto Ready
	AltFire: //new
	GRLG B 4 A_GunFlash
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	GRLG B 8 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
	GRLG B 4 A_GunFlash
	TNT1 A 0 A_SetBlend("Orange",0.042,4)
	GRLG B 8 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
	GRLG B 4 A_GunFlash
	TNT1 A 0 A_SetBlend("Orange",0.044,4)
	GRLG B 8 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
	GRLG B 0 A_ReFire
	Goto Ready
	Flash:
	GRLF A 3 Bright A_Light1
	GRLF B 4 Bright
	GRLF C 4 Bright A_Light2
	GRLF D 4 Bright A_Light2
	Goto LightDone
	}
}

Actor STGrenade
{
Radius 8
Height 8
Speed 25
Damage 20
Gravity 0.25
Projectile
-NOGRAVITY
+DOOMBOUNCE
+DEHEXPLOSION
+GRENADETRAIL
SeeSound "Weapons/GrBnce"
DeathSound "Weapons/GrenLX"
DamageType Fire
States
	{
	Spawn:
	SGRN A 0 Bright
	SGRN A 0 Bright A_StopSound
	SGRN A 0 Bright A_PlaySound("weapons/stgrshot")
	SGRN A 1 Bright
	Goto Spawn+3
	Death:
	MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
	MISL B 8 Bright A_Explode
	MISL C 6 Bright
	MISL D 4 Bright
	stop
	}
}